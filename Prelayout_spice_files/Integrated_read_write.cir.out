*total circuit

.include osu018.lib

*6t cell

*inverters
m2 q qb vdd vdd CMOSP W=0.4u L=0.18u M=1
m5 qb q vdd vdd CMOSP W=0.4u L=0.18u M=1
m3 q qb gnd gnd CMOSN W=0.8u L=0.18u M=1
m4 qb q gnd gnd CMOSN W=0.8u L=0.18u M=1

*access transistors
m1 q wl bl gnd CMOSN W=0.4u L=0.18u M=1
m6 qb wl br gnd CMOSN W=0.4u L=0.18u M=1

* precharge circuit
m7 br pre vdd vdd CMOSP W=0.4u L=0.18u M=1
m8 bl pre vdd vdd  CMOSP W=0.4u L=0.18u M=1
m9 bl pre br vdd CMOSP W=0.4u L=0.18u M=1

*sense amplifier

m10 N002 sen vdd vdd CMOSP l=0.18u w=1.2u

m11 check check N002 vdd CMOSP l=0.18u w=0.4u

m12 amp_out check N002 vdd CMOSP l=0.18u w=0.8u

m13 check br gnd gnd CMOSN l=0.18u w=0.4u

m14 amp_out bl gnd gnd CMOSN l=0.18u w=0.4u

*inverter
m15 dout amp_out vdd vdd CMOSP l=0.18u w=0.8u
m16 dout amp_out gnd gnd CMOSN l=0.18u w=0.4u


*write drivers


*inverters for enable and data input
m17 bl_bar din vdd vdd CMOSP w=0.8u l=0.18u
m18 bl_bar din gnd gnd CMOSN w=0.4u l=0.18u
m19 we_bar we vdd vdd CMOSP w=0.8u l=0.18u
m20 we_bar we gnd gnd CMOSN w=0.4u l=0.18u

*tristate for BL
m21 int1 bl_bar vdd vdd CMOSP w=0.8u l=0.18u
m22 bl we int1 vdd CMOSP w=0.8u l=0.18u
m23 bl we_bar int2 gnd CMOSN w=0.4u l=0.18u
m24 int2 bl_bar gnd gnd CMOSN w=0.4u l=0.18u

*tristate for BR
m25 int3 din vdd vdd CMOSP w=0.8u l=0.18u
m26 br we int3 vdd CMOSP w=0.8u l=0.18u
m27 br we_bar int4 gnd CMOSN w=0.4u l=0.18u
m28 int4 din gnd gnd CMOSN w=0.4u l=0.18u


*voltages


v1 vdd gnd  dc 1.8V

v2 wl gnd pulse(0V 1.8V 0ns 1ns 1ns 50ns 100ns)

v3 pre gnd pulse(1.8V 0V 50ns 1ns 1ns 50ns 100ns)

v6 we gnd pulse(1.8V 0V 0ns 1ns 1ns 50ns 100ns)

v7 sen gnd pulse(0V 1.8V 50ns 1ns 1ns 50ns 100ns)

v8 din gnd pulse(0V 1.8V 0ns 1ns 1ns 10ns 20ns)

.tran 10e-12 200e-09 1e-09

.control
run

plot v(wl)+12 v(we)+10 v(din)+8 v(sen)+6 v(pre)+4  v(dout)

plot v(bl)+2 v(br)


.endc
.end














